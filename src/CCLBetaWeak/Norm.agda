module CCLBetaWeak.Norm where

open import CCLBetaWeak.CCL

open import Data.Unit
  using (âŠ¤ ; tt)
open import Data.Empty
  using (âŠ¥)
open import Data.Product
  using (_Ã—_ ; _,_ ; projâ‚ ; projâ‚‚)
open import Data.Sum
  using (_âŠ_ ; injâ‚ ; injâ‚‚)
open import Relation.Binary.Construct.Closure.ReflexiveTransitive
  using (Star)
  renaming (_â—…â—…_ to trans)

open Star renaming (Îµ to refl)

-- normal identity
idn : Nf a a
idn {ğ•“}     = idğ•“
idn {ğŸ™}     = idğŸ™
idn {a â‡’ b} = up idâ‡’
idn {a * b} = id*

-- interpretation of types
Val : Ty â†’ Ty â†’ Set
Val a ğ•“       = Nf a ğ•“ -- see Note 1
Val a ğŸ™       = Nf a ğŸ™ -- see Note 2
Val a (b â‡’ c) = Nf a (b â‡’ c) Ã— (Val a b â†’ Val a c) -- see Notes 3 and 4
Val a (b * c) = Nf a (b * c) Ã— (Val a b Ã— Val a c)

----
-- Notes on interpretation:
--
-- 1. (ğ•“) We interpret ğ•“ as `Nf a ğ•“` instead of `Ne a b` to preserve `id {ğ•“}`
--
-- 2. (ğŸ™) For the same reason as 1, we interpret ğŸ™ as `Nf a ğŸ™` instead of âŠ¤
--
-- 3. (â‡’) Changing the first component of interpretation to Tm (a * b) c
-- would force all functions to be constructed by `curry`.
-- A notable consequence of this is that it would require
-- identities to be expanded to `curry apply`---which requires
-- an additional (eta-)rule: `id {_â‡’_} âŸ¶ curry (apply)`
--
-- 4. (â‡’) Changing the normal form constructor `curry` to accept
-- a normal form argument (instead of a term) would force
-- full-beta reduction. This also demands a richer interpretation:
-- the second component of interpretation must support weakening (I think)
--
----

-- from semantics to normal forms
reify : Val e a â†’ Nf e a
reify {a = ğ•“}     x       = x
reify {a = ğŸ™}     x       = x
reify {a = a â‡’ b} (n , _) = n
reify {a = a * b} (n , _) = n

-- from semantics to terms
quot : Val a b â†’ Tm a b
quot x = embNf (reify x)

-- from neutrals to semantics (via types)
reflect : Ne a b â†’ Val a b
reflect {b = ğ•“}     t = up t
reflect {b = ğŸ™}     t = up t
reflect {b = b â‡’ c} t = up t , Î» x â†’ reflect {_} {c} (appâˆ™pair t (reify x))
reflect {b = b * c} t = up t , reflect (fstâˆ™ t) , reflect (sndâˆ™ t)

-- semantic identity
id' : Val a a
id' {ğ•“}     = idğ•“
id' {ğŸ™}     = idğŸ™
id' {a â‡’ b} = up idâ‡’ , (Î» x â†’ reflect (appâˆ™pair idâ‡’ (reify x)))
id' {a * b} = id* , reflect fst , reflect snd

infixr 4 _âˆ˜_

-- semantic application
_âˆ˜_ : Val a (b â‡’ c) â†’ Val a b â†’ Val a c
(_ , f) âˆ˜ x = f x

-- semantic projection fst composition
fstâˆ™' : Val a (b * c) â†’ Val a b
fstâˆ™' (_ , x , _) = x

-- semantic projection snd composition
sndâˆ™' : Val a (b * c) â†’ Val a c
sndâˆ™' (_ , _ , x) = x

-- semantic application composition
applyâˆ™' : Val a ((b â‡’ c) * b) â†’ Val a c
applyâˆ™' (_ , f , x) = f âˆ˜ x

-- semantic pairing
pair' : Val a b â†’ Val a c â†’ Val a (b * c)
pair' x y = pair (reify x) (reify y) , x , y

-- semantic term composition
evalâˆ™ : Tm a b â†’ Val e a â†’ Val e b
evalâˆ™ id      x = x
evalâˆ™ (t âˆ™ u) x = evalâˆ™ t (evalâˆ™ u x)
evalâˆ™ unit    x = unit
evalâˆ™ fst     x = fstâˆ™' x
evalâˆ™ snd     x = sndâˆ™' x
evalâˆ™ apply   x = applyâˆ™' x
evalâˆ™ (pair t u) x
  = pair (reify (evalâˆ™ t x)) (reify (evalâˆ™ u x))
  , (evalâˆ™ t x)
  , (evalâˆ™ u x)
evalâˆ™ (curry t) x
  = curry (t âˆ™ quot x âŠ— id)
  , Î» y â†’ evalâˆ™ t (pair (reify x) (reify y) , x , y)

-- interpretation of terms
eval : Tm a b â†’ Val a b
eval id         = id'
eval (t âˆ™ u)    = evalâˆ™ t (eval u)
eval fst        = reflect fst
eval snd        = reflect snd
eval unit       = unit
eval apply      = reflect apply
eval (pair t u) = pair' (eval t) (eval u)
eval (curry t)  = (curry t) , Î» x â†’ evalâˆ™ t (pair idn (reify x) , id' , x)

-- normalization function
norm : Tm a b â†’ Tm a b
norm t = quot (eval t)

--------------------------
-- Logging reduction trace
--------------------------

-- trace builder
R : Tm a b â†’ Val a b â†’ Set
R {a} {ğ•“}      t x = t âŸ¶* quot x
R {a} {ğŸ™}      t x = t âŸ¶* quot x
R {a} {b â‡’ c}  t f = t âŸ¶* quot f
  Ã— ({u : Tm a b} {u' : Val a b}
  â†’ R u u'
  â†’ R (apply âˆ™ pair t u) (applyâˆ™' (pair' f u')))
R {a} {b * c} t x = t âŸ¶* quot x
  Ã— R (fst âˆ™ t) (fstâˆ™' x)
  Ã— R (snd âˆ™ t) (sndâˆ™' x)

-- extract trace
R-reduces : {t : Tm a b} {x : Val a b}
  â†’ R t x
  â†’ t âŸ¶* quot x
R-reduces {b = ğ•“}     p = p
R-reduces {b = ğŸ™}     p = p
R-reduces {b = b â‡’ c} p = projâ‚ p
R-reduces {b = b * c} p = projâ‚ p

-- chain trace with a builder
R-chain : {f g : Tm a b} {x : Val a b}
  â†’ g âŸ¶* f
  â†’ R f x
  â†’ R g x
R-chain {b = ğ•“} gâŸ¶*f fâŸ¶*x
  = trans gâŸ¶*f fâŸ¶*x
R-chain {b = ğŸ™} gâŸ¶*f fâŸ¶*x
  = trans gâŸ¶*f fâŸ¶*x
R-chain {b = b â‡’ c} gâŸ¶*f (fâŸ¶*x , ss)
  = trans gâŸ¶*f fâŸ¶*x
  , Î» {u} {y} uRy
    â†’ R-chain
      (cong-âˆ™ refl (cong-pair gâŸ¶*f refl))
      (ss uRy)
R-chain {b = b * c} gâŸ¶*f (fâŸ¶*x , sc1 , sc2)
  = trans gâŸ¶*f fâŸ¶*x
  , R-chain (cong-âˆ™ refl gâŸ¶*f) sc1
  , R-chain (cong-âˆ™ refl gâŸ¶*f) sc2

-- trace application composition
R-applyâˆ™ : {u : Tm e ((a â‡’ b) * a)} {v : Val e ((a â‡’ b) * a)}
  â†’ R u v
  â†’ R (apply âˆ™ u) (applyâˆ™' v)
R-applyâˆ™ (p , (q , ss) , r)
  = R-chain (lift exp-applyâˆ™) (ss r)

-- lemma for reducing function application
beta-lemma :
 {t : Tm (b * c) d} {u1 : Tm a b} {u2 : Tm a c }
 â†’ (apply âˆ™ pair (curry (t âˆ™ (u1 âŠ— id))) u2)
   âŸ¶* t âˆ™ pair u1 u2
beta-lemma = trans
  (lift red-apply)
  (trans
    (lift assoc)
     (cong-âˆ™
       refl
       (trans
         (lift comp-pair)
         (cong-pair
           (trans (lift assoc) (trans (cong-âˆ™ refl (lift red-fst)) (lift red-idr)))
           (trans (lift assoc) (trans (cong-âˆ™ refl (lift red-snd)) (lift red-idl)))))))

-- trace builder for composition
Fundâˆ™ : {a b : Ty} (t : Tm a b) â†’ Set
Fundâˆ™ {a} {b} t = âˆ€ {e} {u : Tm e a} {v : Val e a}
  â†’ R u v
  â†’ R (t âˆ™ u) (evalâˆ™ t v)

-- trace composition
fundâˆ™ : (t : Tm a b) â†’ Fundâˆ™ t
fundâˆ™ id         uRv
  = R-chain (lift red-idl) uRv
fundâˆ™ (t âˆ™ u)    uRv
  = R-chain (lift assoc) (fundâˆ™ t (fundâˆ™ u uRv))
fundâˆ™ fst        (_ , p , _) = p
fundâˆ™ snd        (_ , _ , p) = p
fundâˆ™ (pair t t') uRv
  = trans
      (lift comp-pair)
      (cong-pair
        (R-reduces (fundâˆ™ t uRv))
        (R-reduces (fundâˆ™ t' uRv)))
  , R-chain
      (trans (cong-âˆ™ refl (lift comp-pair)) (lift red-fst))
      (fundâˆ™ t uRv)
  , R-chain
      (trans (cong-âˆ™ refl (lift comp-pair)) (lift red-snd))
      (fundâˆ™ t' uRv)
fundâˆ™ unit       uRv = lift red-unit
fundâˆ™ (curry t)  uRv
  = trans
      (cong-âˆ™ refl (R-reduces uRv))
      (lift comp-curry)
  , Î» {u = s} {x} sRx â†’ R-chain
        (trans
          (cong-âˆ™ refl (cong-pair (lift comp-curry) refl))
          beta-lemma)
        (fundâˆ™ t
          ((cong-pair (R-reduces uRv) (R-reduces sRx))
          , ((R-chain (lift red-fst) uRv)
          , (R-chain (lift red-snd) sRx))))
fundâˆ™ apply {u = u} {v = v} uRv
  = R-applyâˆ™ {u = u} {v = v} uRv

-- trace reflection
R-reflect : (n : Ne a b)
  â†’ R (embNf (up n)) (reflect n)
R-reflect {b = ğ•“} n = refl
R-reflect {b = ğŸ™} n = refl
R-reflect {b = b â‡’ bâ‚} n = refl , Î» {u = u} {v} uRv
  â†’ R-chain
      (cong-âˆ™ refl (cong-pair refl (R-reduces uRv)))
      (R-reflect (appâˆ™pair n (reify v)))
R-reflect {b = b * bâ‚} n = refl , (R-reflect (fstâˆ™ n) , R-reflect (sndâˆ™ n))

-- trace identity
R-id : R {a} id id'
R-id {ğ•“} = refl
R-id {ğŸ™} = refl
R-id {a â‡’ b}
  = refl
  , Î» {u = u} {v} uRv
    â†’ R-chain
        (cong-âˆ™ refl (cong-pair refl (R-reduces uRv)))
        (R-reflect (appâˆ™pair idâ‡’ (reify v)))
R-id {a * b}
  = refl
  , R-chain (lift red-idr) (R-reflect fst)
  , R-chain (lift red-idr) (R-reflect snd)

-- identity reduces to normal identity
idâŸ¶*idn : id âŸ¶* embNf {a} idn
idâŸ¶*idn {ğ•“} = refl
idâŸ¶*idn {ğŸ™} = refl
idâŸ¶*idn {a â‡’ b} = refl
idâŸ¶*idn {a * b} = refl

-- trace pairing
R-pair : {t : Tm a b} {x : Val a b}
  {u : Tm a c} {y : Val a c}
  â†’ R t x
  â†’ R u y
  â†’ R (pair t u) (pair' x y)
R-pair tRx uRy
  = (cong-pair (R-reduces tRx) (R-reduces uRy))
  , (R-chain (lift red-fst) tRx)
  , (R-chain (lift red-snd) uRy)

-- trace interpretation
fund : (t : Tm a b) â†’ R t (eval t)
fund id = R-id
fund (t âˆ™ u) = fundâˆ™ t (fund u)
fund fst = R-reflect fst
fund snd = R-reflect snd
fund (pair t u) = R-pair (fund t) (fund u)
fund unit = refl
fund (curry t) = refl , (Î» {u = u} {v} uRv
  â†’ R-chain
      (lift red-apply)
      (fundâˆ™ t (cong-pair idâŸ¶*idn (R-reduces uRv)
      , R-chain (lift red-fst) R-id
      , R-chain (lift red-snd) uRv)))
fund apply = R-reflect apply

-- trace normalization
trace : (t : Tm a b) â†’ t âŸ¶* norm t
trace t = R-reduces (fund t)
