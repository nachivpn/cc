module CCLBetaWeak.CCL where

open import Relation.Binary.Construct.Closure.ReflexiveTransitive
  using (Star)
  renaming (_â—…â—…_ to trans)

open Star renaming (Îµ to refl)

infixr 5 _â‡’_

data Ty : Set where
  ğ•“        : Ty
  ğŸ™        : Ty
  _â‡’_  _*_ : (a b : Ty) â†’ Ty

variable
  a b c d e : Ty

infixr 4 _âˆ™_

data Tm : (a b : Ty) â†’ Set where
  id    : Tm a a
  _âˆ™_   : Tm b c â†’ Tm a b â†’ Tm a c
  fst   : Tm (a * b) a
  snd   : Tm (a * b) b
  pair  : Tm a b â†’ Tm a c â†’ Tm a (b * c)
  unit  : Tm a ğŸ™
  curry : Tm (c * a) b â†’ Tm c (a â‡’ b)
  apply : Tm ((a â‡’ b) * a) b

_âŠ—_ : Tm a c â†’ Tm b d â†’ Tm (a * b) (c * d)
t âŠ— u = pair (t âˆ™ fst) (u âˆ™ snd)

infix 3 _âŸ¶_
infix 3 _âŸ¶*_

-- single-step reduction
data _âŸ¶_ : Tm a b â†’ Tm a b â†’ Set where

  -- reduction rules
  red-idl  : {f : Tm a b}
    â†’ id âˆ™ f âŸ¶ f
  red-idr  : {f : Tm a b}
    â†’ f âˆ™ id âŸ¶ f
  red-fst  : {f : Tm a b} {g : Tm a c}
    â†’ fst âˆ™ (pair f g) âŸ¶ f
  red-snd  : {f : Tm a b} {g : Tm a c}
    â†’ snd âˆ™ (pair f g) âŸ¶ g
  red-apply : {f : Tm (a * b) c} {g : Tm a b}
    â†’ apply âˆ™ pair (curry f) g âŸ¶ (f âˆ™ pair id g)
  red-unit : {f : Tm a b}
    â†’ (unit âˆ™ f) âŸ¶ unit

  -- composition rules
  assoc : {f : Tm c d} {g : Tm b c} {h : Tm a b}
    â†’ (f âˆ™ g) âˆ™ h âŸ¶ f âˆ™ (g âˆ™ h)
  comp-pair : {f : Tm b c} {g : Tm b d} {h : Tm a b}
    â†’ pair f g âˆ™ h âŸ¶ pair (f âˆ™ h) (g âˆ™ h)
  comp-curry : {h : Tm d c} {f : Tm (c * a) b}
    â†’ curry f âˆ™ h âŸ¶ curry (f âˆ™ pair (h âˆ™ fst) snd)

  -- "surjective pairing" restricted to application site
  exp-apply :
    apply {a} {b} âŸ¶ apply âˆ™ pair fst snd

  -- congruence rules
  cong-pair1 : {f f' : Tm a b} {g : Tm a c}
    â†’ f âŸ¶ f'
    â†’ (pair f g) âŸ¶ (pair f' g)
  cong-pair2 : {f : Tm a b} {g g' : Tm a c}
    â†’ g âŸ¶ g'
    â†’ (pair f g) âŸ¶ (pair f g')

  cong-âˆ™1 : {f f' : Tm b c} {g : Tm a b}
    â†’ f âŸ¶ f'
    â†’ f âˆ™ g âŸ¶ f' âˆ™ g

  cong-âˆ™2 : {f : Tm b c} {g g' : Tm a b}
    â†’ g âŸ¶ g'
    â†’ f âˆ™ g âŸ¶ f âˆ™ g'

  -- NOTE: there is no congruence rule for curry
  -- since we want weak-head normal forms for functions

-- multi-step reduction
_âŸ¶*_ : Tm a b â†’ Tm a b â†’ Set
_âŸ¶*_ = Star (_âŸ¶_)

-- embed âŸ¶ to âŸ¶*
lift : {t t' : Tm a b}
  â†’ t âŸ¶ t'
  â†’ t âŸ¶* t'
lift p = p â—… refl

cong-pair : {f f' : Tm a b} {g g' : Tm a c}
    â†’ f âŸ¶* f'
    â†’ g âŸ¶* g'
    â†’ (pair f g) âŸ¶* (pair f' g')
cong-pair refl refl = refl
cong-pair refl (x â—… q) = cong-pair2 x â—… cong-pair refl q
cong-pair (x â—… p) q = cong-pair1 x â—… cong-pair p q

cong-âˆ™ : {f f' : Tm b c} {g g' : Tm a b}
  â†’ f âŸ¶* f'
  â†’ g âŸ¶* g'
  â†’ f âˆ™ g âŸ¶* f' âˆ™ g'
cong-âˆ™ refl refl = refl
cong-âˆ™ refl (x â—… q) = cong-âˆ™2 x â—… cong-âˆ™ refl q
cong-âˆ™ (x â—… p) q = cong-âˆ™1 x â—… cong-âˆ™ p q

-- reflectable terms
data Ne : Ty â†’ Ty â†’ Set

-- reifiable terms
data Nf : Ty â†’ Ty â†’ Set

embNe : Ne a b â†’ Tm a b
embNf : Nf a b â†’ Tm a b

data Ne where

  -- fst , snd
  fst      : Ne (a * b) a
  snd      : Ne (a * b) b

  -- fst âˆ™ n
  fstâˆ™     : Ne a (b * c) â†’ Ne a b

  -- snd âˆ™ n
  sndâˆ™     : Ne a (b * c) â†’ Ne a c

  -- contract of `curry (apply)`
  idâ‡’      : Ne (a â‡’ b) (a â‡’ b)

  -- apply âˆ™ pair n m
  appâˆ™pair : Ne a (b â‡’ c) â†’ Nf a b â†’ Ne a c

data Nf where

  -- embed neutrals to normals
  up    : Ne a b â†’ Nf a b

  -- canonical identity for ğ•“
  idğ•“   : Nf ğ•“ ğ•“

  -- contract of unit
  idğŸ™   : Nf ğŸ™ ğŸ™

  -- contract of (pair id id)
  id*   : Nf (a * b) (a * b)

  -- value constructors
  unit  : Nf a ğŸ™
  pair  : Nf a b â†’ Nf a c â†’ Nf a (b * c)
  curry : Tm (a * b) c â†’ Nf a (b â‡’ c)

embNe fst            = fst
embNe snd            = snd
embNe (fstâˆ™ t)       = fst âˆ™ embNe t
embNe (sndâˆ™ t)       = snd âˆ™ embNe t
embNe idâ‡’            = id
embNe (appâˆ™pair t u) = apply âˆ™ pair (embNe t) (embNf u)

embNf (up x)     = embNe x
embNf idğ•“        = id
embNf idğŸ™        = id
embNf id*        = id
embNf unit       = unit
embNf (pair m n) = pair (embNf m) (embNf n)
embNf (curry t)  = curry t
